apply from: "version.gradle"
ext {
    /**
     * host
     */
    host = [
            /**
             * 生产环境
             */
            host_prod: 'iwtoaok.iask.in:41346',
            /**
             * 开发环境
             */
            host_dev : '127.0.0.1:8080',
    ]
    /**
     * 默认环境
     */
    host_default = host.host_dev

    /**
     * release 生产 环境
     */
    host_prod = host.host_prod

    configAndroidDomain = this.&configAndroidDomain
    configBaseDependences = this.&configBaseDependences
    configModuleBaseDependences = this.&configModuleBaseDependences
    configLibDependences = this.&configLibDependences

    /**
     * 启动页图片资源
     */
    splash_resource = [
            splash_url         : 'https://baijiahao.baidu.com/s?id=1574517843319982&wfr=spider&for=pc&isFailFlag=1',
            splash_cssquery    : 'div.article-content>div.img-container>img.large',
            splash_attributekey: 'src'
    ]
}

def configBaseDependences(Project pro) {
    pro.dependencies {

        implementation baseDepCfg.appcompat_v7
        implementation baseDepCfg.constraint_layout
        implementation baseDepCfg.design

        testImplementation baseDepCfg.junit

        androidTestImplementation baseDepCfg.runner
        androidTestImplementation baseDepCfg.espresso_core
    }
}

def configLibDependences(Project pro) {
    pro.dependencies {

        /**
         * Lib
         */
        //rx
        implementation depLibConfig.rxjava
        implementation depLibConfig.rxandroid

        //okhttp
        implementation depLibConfig.okhttp

        //retrofit
        implementation depLibConfig.retrofit
        implementation depLibConfig.converter_gson
        implementation depLibConfig.adapter_rxjava

        //jsoup
        implementation depLibConfig.jsoup

        //rxpermissions
        implementation depLibConfig.rxpermissions

        /**
         * jar
         */

        /**
         * module
         */
        /*
         * leaks 必须使用 debugImplementation 来依赖，不然会解析失败
         * 这是因为在子模块中进行注册依赖是也用的是debugImplementation
         */
        debugImplementation project(':Leaks')
        //The MVP
        implementation project(':Themvp')
        //android utils
        implementation project(':Utils')
        //ImageLoader
        implementation project(':Imageloder')
    }
}

def configAndroidDomain(Project pro) {
    if (pro.plugins.hasPlugin("com.android.application")) {
        configAppAndroidDomain(pro)
    } else {
        configLibAndroidDomain(pro)
    }
}


def configAppAndroidDomain(Project pro) {
    configDefault(pro)
    configField(pro)
    configSigningAndBuildType(pro)
    configProductFlavors(pro)
    configApkName(pro)
    configSourceSets(pro)
    configOption(pro)
}

//config Option
def configOption(Project pro) {
    pro.android {
        lintOptions {
            abortOnError false
        }

        dexOptions {
//            incremental = true
//            javaMaxHeapSize "4g"
//            jumboMode = true
        }
        //打包时排出以下文件
        packagingOptions {
            exclude 'META-INF/rxjava.properties'
        }
    }

}

//config sourceSets
def configSourceSets(Project pro) {
    pro.android.sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            //自定义资源文件目录，注意备份
            res.srcDirs =
                    [
                            'src/main/res/main',
                            'src/main/res/'
                    ]
        }
        // special build type variants
        dev.setRoot('src/buildtype/dev')
        debug.setRoot('src/buildtype/debug')
    }
}

//config signing and buildType
def configSigningAndBuildType(Project pro) {
    File signPropertiesFile = file('sign/keystore.properties')
    if (!signPropertiesFile.exists()) return
    pro.android {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signPropertiesFile))
        signingConfigs {
            release {
                storeFile file(properties['keystore'])
                storePassword(properties['storePassword'])
                keyAlias(properties['keyAlias'])
                keyPassword(properties['keyPassword'])
                v1SigningEnabled true
                v2SigningEnabled false
            }
        }
        buildTypes {
            /*
           * minifyEnabled　是否开启混淆
           * zipAlignEnabled　是否优化apk文件，将apk文件中未压缩的数据在4个字节边界上对齐，具体见改善android性能工具：Zipalign
           * shrinkResources 是否去除无用资源，任何在编译过程中没有用到的资源或者代码都会被删除，可以有效减小apk体积
           * proguardFiles 指定混淆规则文件
           * */
            release {
                signingConfig signingConfigs.release
                minifyEnabled true
                shrinkResources true
                zipAlignEnabled true

                buildConfigField "String", "API_HOST", "\"${host_prod}\""
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }

            dev {
                signingConfig signingConfigs.release
                applicationIdSuffix ".dev"
                minifyEnabled true
                shrinkResources true
                zipAlignEnabled true
                buildConfigField "String", "API_HOST", "\"${host_default}\""
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }

            debug {
                signingConfig signingConfigs.release
                applicationIdSuffix ".debug"
                buildConfigField "String", "API_HOST", "\"${host_default}\""
            }
        }
    }
}

//config ProductFlavors
def configProductFlavors(Project pro) {
    pro.android.productFlavors {
        androidDemo {}

    }
}

//config defaltConfig
def configDefault(Project pro) {
    pro.android {
        compileSdkVersion androidConfig.compileSdkVersion
        defaultConfig {
            applicationId androidConfig.applicationId
            minSdkVersion androidConfig.minSdkVersion
            targetSdkVersion androidConfig.targetSdkVersion
            versionCode androidConfig.versionCode
            versionName androidConfig.versionName
            //flavorDimensions
            flavorDimensions androidConfig.flavorDimensions


            //自定义常量字段
            buildConfigField "String", "SPLASH_URL", "\"${splash_resource.splash_url}\""
            buildConfigField "String", "SPLASH_CSSQUERY", "\"${splash_resource.splash_cssquery}\""
            buildConfigField "String", "SPLASH_ATTRIBUTEKEY", "\"${splash_resource.splash_attributekey}\""
        }
    }

}

//config Field
def configField(Project pro) {
    pro.android.defaultConfig {
        resValue "string", "app_name", androidConfig.appName
    }
}

//config apk name
def configApkName(Project pro) {

    pro.android.applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {//不做判断，会在debug时不能直接调试安装
            variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk")
            variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
                apkData.outputFileName = androidConfig.appName + "_" + getUpperCase(variant.buildType.name) + "_" + variant.versionName.replace(".", "_") + ".apk"
            }
        }
    }
}

//大写
def getUpperCase(String word) {
    return word.substring(0, 1).toUpperCase() + word.substring(1, word.length())
}

def configLibAndroidDomain(Project pro) {
    pro.android {
        compileSdkVersion androidConfig.compileSdkVersion
        defaultConfig {
            minSdkVersion androidConfig.minSdkVersion
            targetSdkVersion androidConfig.targetSdkVersion
            versionCode androidConfig.versionCode
            versionName androidConfig.versionName
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
            dev {}
            debug {}
        }
        lintOptions {
            abortOnError false
        }
    }
}

def configModuleBaseDependences(Project pro) {
    pro.dependencies {
        compileOnly baseDepCfg.appcompat_v7
        compileOnly baseDepCfg.constraint_layout

        testImplementation baseDepCfg.junit

        androidTestImplementation baseDepCfg.runner
        androidTestImplementation baseDepCfg.espresso_core
    }
}