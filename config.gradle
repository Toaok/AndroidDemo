import java.text.DateFormat
import java.text.SimpleDateFormat

apply from: "version.gradle"
ext {
    /**
     * host
     */
    host = [
            /**
             * 生产环境
             */
            host_prod: 'iwtoaok.iask.in:41346',
            /**
             * 开发环境
             */
            host_dev : '127.0.0.1:8080',
    ]
    /**
     * 默认环境
     */
    host_default = host.host_dev

    /**
     * release 生产 环境
     */
    host_prod = host.host_prod

    /**
     * 启动页图片资源
     */
    splash_resource = [
            splash_url         : 'https://baijiahao.baidu.com/s?id=1574517843319982&wfr=spider&for=pc&isFailFlag=1',
            splash_cssquery    : 'div.article-content>div.img-container>img.large',
            splash_attributekey: 'src'
    ]

    configAndroidDomain = this.&configAndroidDomain
    configAppBaseDependences = this.&configAppBaseDependence
    configModuleBaseDependences = this.&configModuleBaseDependences
    configLibDependences = this.&configLibDependences

}

//app base dependence
def configAppBaseDependence(Project pro) {
    pro.dependencies {

        implementation baseDepCfg.appcompat_v7
        implementation baseDepCfg.constraint_layout
        implementation baseDepCfg.design

        implementation baseDepCfg.multidex

        testImplementation baseDepCfg.junit

        androidTestImplementation baseDepCfg.runner
        androidTestImplementation baseDepCfg.espresso_core
    }
}

//app dependences
def configLibDependences(Project pro) {
    pro.dependencies {

        /**
         * Lib
         */
        //rx
        implementation depLibConfig.rxjava
        implementation depLibConfig.rxandroid

        //okhttp
        implementation depLibConfig.okhttp

        //retrofit
        implementation depLibConfig.retrofit
        implementation depLibConfig.converter_gson
        implementation depLibConfig.adapter_rxjava

        //jsoup
        implementation depLibConfig.jsoup

        //rxpermissions
        implementation depLibConfig.rxpermissions

        //gaode
        implementation depLibConfig.gaode_navi
        implementation depLibConfig.gaode_location
        implementation depLibConfig.gaode_search

        /**
         * jar
         */

        /**
         * module
         */
        /*
         * leaks 必须使用 debugImplementation 来依赖，不然会解析失败
         * 这是因为在子模块中进行注册依赖是也用的是debugImplementation
         */
        debugImplementation project(':Leaks')
        //The MVP
        implementation project(':Themvp')
        //android utils
        implementation project(':Utils')
        //ImageLoader
        implementation project(':Imageloder')
    }
}
//app domain
def configAndroidDomain(Project pro) {
    if (pro.plugins.hasPlugin("com.android.application")) {
        configAppAndroidDomain(pro)
    } else {
        configModuleAndroidDomain(pro)
    }
}

//app main
def configAppAndroidDomain(Project pro) {
    configDefault(pro)
    configField(pro)
    configNdk(pro)
    configSigningAndBuildType(pro)
    configBuildType(pro)
    configProductFlavors(pro)
    configApkName(pro)
    configSourceSets(pro)
    configOption(pro)
}

//config Option
def configOption(Project pro) {
    pro.android {
        lintOptions {
            abortOnError false
        }

        dexOptions {
//            incremental = true
//            javaMaxHeapSize "4g"F
//            jumboMode = true
        }
        //打包时排出以下文件
        packagingOptions {
            exclude 'META-INF/rxjava.properties'
        }
    }

}

//config sourceSets
def configSourceSets(Project pro) {
    pro.android.sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            //自定义资源文件目录，注意备份
            res.srcDirs =
                    [
                            'src/main/res/',
                            'src/main/res/main',
                            'src/main/res/map',
                            'src/main/res/kline',
                            'src/main/res/other'
                    ]
        }
        // special build type variants
        debug.setRoot('src/buildtype/debug')
    }
}

//config signing
def configSigningAndBuildType(Project pro) {
    File signPropertiesFile = file('sign/keystore.properties')
    if (!signPropertiesFile.exists()) return
    pro.android {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signPropertiesFile))
        signingConfigs {
            release {
                storeFile file(properties['keystore'])
                storePassword(properties['storePassword'])
                keyAlias(properties['keyAlias'])
                keyPassword(properties['keyPassword'])
                v2SigningEnabled true
            }
        }
    }
}

//config build type
def configBuildType(Project pro) {
    pro.android {
        buildTypes {
            /*
           * minifyEnabled　是否开启混淆
           * zipAlignEnabled　是否优化apk文件，将apk文件中未压缩的数据在4个字节边界上对齐，具体见改善android性能工具：Zipalign
           * shrinkResources 是否去除无用资源，任何在编译过程中没有用到的资源或者代码都会被删除，可以有效减小apk体积
           * proguardFiles 指定混淆规则文件
           * */
            release {
                signingConfig signingConfigs.release
                minifyEnabled true
                shrinkResources true
                zipAlignEnabled true
                debuggable false

                buildConfigField "String", "API_HOST", "\"${host_prod}\""
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }

            debug {
                signingConfig signingConfigs.release
                applicationIdSuffix ".debug"
                buildConfigField "String", "API_HOST", "\"${host_default}\""
            }
        }
    }
}

//config ProductFlavors
def configProductFlavors(Project pro) {
    pro.android.productFlavors {
        androidDemo {}

    }
}

//config defaltConfig
def configDefault(Project pro) {
    pro.android {
        compileSdkVersion androidConfig.compileSdkVersion
        defaultConfig {
            applicationId androidConfig.applicationId
            minSdkVersion androidConfig.minSdkVersion
            targetSdkVersion androidConfig.targetSdkVersion
            versionCode androidConfig.versionCode
            versionName androidConfig.versionName

            //分割 Dex 文件
            multiDexEnabled true

            //flavorDimensions
            flavorDimensions androidConfig.flavorDimensions
        }
    }

}

//config Field
def configField(Project pro) {

    pro.android.defaultConfig {

        //设置高德APPKEY到Manifest中
        manifestPlaceholders.put("GAODE_KEY", rootProject.ext.gaode.APP_KEY)

        //自定义常量字段
        buildConfigField "String", "SPLASH_URL", "\"${splash_resource.splash_url}\""
        buildConfigField "String", "SPLASH_CSSQUERY", "\"${splash_resource.splash_cssquery}\""
        buildConfigField "String", "SPLASH_ATTRIBUTEKEY", "\"${splash_resource.splash_attributekey}\""

        resValue "string", "app_name", androidConfig.appName
    }
}

def configNdk(Project pro) {
    pro.android.defaultConfig {
        ndk {
            //设置支持的SO库架构
            abiFilters
            'armeabi'
            'armeabi-v7a'
            'arm64-v8a'
            'x86'
            'x86_64'
        }
    }
}

//config apk name
def configApkName(Project pro) {
    pro.android.applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {//在debug时不重命名安装包
            variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk")
            variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
                apkData.outputFileName = androidConfig.appName + //APP名称
                        "_" + getUpperCase(variant.buildType.name) + //打包方式
                        "_" + variant.versionName + //APP版本
                        //"_" + getCurrentTime() + //打包时间
                        ".apk"
            }
        }
    }
}

//module config
def configModuleAndroidDomain(Project pro) {
    pro.android {
        compileSdkVersion androidConfig.compileSdkVersion
        defaultConfig {
            minSdkVersion androidConfig.minSdkVersion
            targetSdkVersion androidConfig.targetSdkVersion
            versionCode androidConfig.versionCode
            versionName androidConfig.versionName
        }
        buildTypes {
            /*
            * Note：
            * 子module中混淆与主module不一样，如要使用子module的混淆文件则应使用consumerProguardFiles [混淆文件名]
            * consumerProguardFiles 'proguard-rules.pro'
            * */
            release {
                minifyEnabled true
                consumerProguardFiles 'proguard-rules.pro'
            }
            debug {
                minifyEnabled false
            }
        }

        lintOptions {
            abortOnError false
        }
    }
}

//module base dependences
def configModuleBaseDependences(Project pro) {
    pro.dependencies {
        compileOnly baseDepCfg.appcompat_v7
        compileOnly baseDepCfg.constraint_layout

        testImplementation baseDepCfg.junit
        androidTestImplementation baseDepCfg.runner
        androidTestImplementation baseDepCfg.espresso_core
    }
}

//frist words uppercase
def static getUpperCase(String word) {
    return word.substring(0, 1).toUpperCase() + word.substring(1, word.length())
}

//get current time
def static getCurrentTime() {
    DateFormat dateFormat = new SimpleDateFormat("YYYY-MM-dd HH-mm-ss")
    String currentTime = dateFormat.format(new Date())
    return currentTime
}
